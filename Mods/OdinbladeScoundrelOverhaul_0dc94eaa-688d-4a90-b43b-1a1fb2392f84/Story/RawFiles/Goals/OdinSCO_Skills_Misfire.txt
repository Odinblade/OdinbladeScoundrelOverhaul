Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
CharacterUsedSkillOnTarget(_Character, _Target, "Projectile_OdinSCO_Misfire", _, _)
AND
HasActiveStatus(_Character, "OdinSCO_USE_COMBO_3", 1)
THEN
OBSCO_LUA_ConsumeComboPoints(_Character);
DB_OBSCO_Misfire(_Character, _Target);

//Prevent the skill cast and put it on a 1-turn CD
IF
NRD_OnActionStateEnter(_Target, "PrepareSkill")
AND
HasActiveStatus(_Target, "OdinSCO_MISFIRE", 1)
AND
NRD_ActionStateGetString(_Target, "SkillId", _ProtoSkillId)
AND
OBSCO_LUA_GetSkillEntryName(_ProtoSkillId, _SkillId)
AND
GetPosition(_Target, _X, _Y, _Z)
AND
StringConcatenate("MISFIRE: Fumbled ", _SkillId, _DebugString)
THEN
CharacterStatusText(_Target, "Spell <font color='#7F00FF'>Fumbled</font>!");
CharacterUseSkill(_Target, "Shout_OdinSCO_Misfire_Proc", _Target, 1, 1, 1);
RemoveStatus(_Target, "OdinSCO_MISFIRE");
NRD_SkillSetCooldown(_Target, _SkillId, 6.0);
OBSCO_Misfire(_Target);

//Perform the damage effect on Fumble
PROC
OBSCO_Misfire((CHARACTERGUID)_Target)
AND
DB_OBSCO_Misfire(_Character, _Target)
AND
GetPosition(_Target, _X, _Y, _Z)
AND
RealSum(_Y, 1.0, _YOffset)
THEN
NRD_ProjectilePrepareLaunch();
NRD_ProjectileSetString("SkillId", "Projectile_OdinSCO_Misfire_Damage");
NRD_ProjectileSetVector3("SourcePosition", _X, _YOffset, _Z);
NRD_ProjectileSetVector3("TargetPosition", _X, _Y, _Z);
NRD_ProjectileSetGuidString("Caster", _Character);
NRD_ProjectileSetGuidString("Target", _Target);
NRD_ProjectileLaunch();

IF
CharacterStatusRemoved(_Target, "OdinSCO_MISFIRE", _)
AND
DB_OBSCO_Misfire(_Character, _Target)
THEN
NOT DB_OBSCO_Misfire(_Character, _Target);
EXITSECTION

ENDEXITSECTION

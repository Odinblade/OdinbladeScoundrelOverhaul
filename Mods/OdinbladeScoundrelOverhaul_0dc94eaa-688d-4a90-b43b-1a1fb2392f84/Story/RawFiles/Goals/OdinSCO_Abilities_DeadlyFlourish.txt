Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//If a character has a Scoundrel skill, give them the Deadly Flourish skill on start
IF
GameStarted(_, _)
AND
DB_IsPlayer(_Player)
THEN
OBSCO_HandleDeadlyFlourish();

//If a savegame character has a Scoundrel skill, ensure they have the Deadly Flourish skill on load
IF
SavegameLoaded(_, _, _, _)
AND
DB_IsPlayer(_Player)
THEN
OBSCO_HandleDeadlyFlourish();

//If a character has any recognised Scoundrel skills, give them Deadly Flourish and notify
PROC
OBSCO_HandleDeadlyFlourish()
AND
DB_IsPlayer(_Character)
AND
CharacterHasSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 0)
AND
OBSCO_LUA_HasScoundrelSkills(_Character, 1)
THEN
CharacterAddSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 1);

//If a character activates their first Scoundrel skill, give them Deadly Flourish and notify
IF
SkillActivated(_Character, _SkillId)
AND
DB_IsPlayer(_Character)
AND
CharacterHasSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 0)
AND
OBSCO_LUA_HasScoundrelSkills(_Character, 1)
THEN
CharacterAddSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 1);

//If a character learns their first Scoundrel skill, give them Deadly Flourish and notify
IF
SkillAdded(_Character, _SkillId, _Learned)
AND
DB_IsPlayer(_Character)
AND
CharacterHasSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 0)
AND
OBSCO_LUA_IsScoundrelSkill(_SkillId, 1)
THEN
CharacterAddSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 1);

//If a character removes their last active Scoundrel skill, remove Deadly Flourish
IF
SkillDeactivated(_Character, _SkillId)
AND
CharacterHasSkill(_Character, "Shout_OdinSCO_DeadlyFlourish", 1)
AND
OBSCO_LUA_HasScoundrelSkills(_Character, 0)
THEN
CharacterRemoveSkill(_Character, "Shout_OdinSCO_DeadlyFlourish");

// Enemy skill
IF
ObjectEnteredCombat((CHARACTERGUID)_Character,_)
AND
NOT DB_IsPlayer(_Character)
AND
OBSCO_LUA_HasScoundrelSkills(_Character, 1)
THEN
CharacterAddSkill(_Character, "Shout_OdinSCO_EnemyDeadlyFlourish", 1);
EXITSECTION

ENDEXITSECTION
